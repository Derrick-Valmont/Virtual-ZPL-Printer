^XA

^FX Set label size to 4" x 6" (assumes 8 dpmm)
^LL 1218
^PW 812

^FX Set the Font/Encoding to
^FX Unicode (UTF-8 encoding) - Unicode Character Set
^CI 28

^FX Shift the label up and to the right
^LT 0
^LS 0

^FX Vertical line for service icon block
^FO 203.2,0
^GB 1,203,3,B
^FS

^FX Service Block (P Priority Mail)
^FO 0,20
^FB 268.2,1,0,C,0
^A0 N,210,210
^FD P
^FS

^FX Payment Block - 20mm 
^FO 533,25.6
^GB 228,152,2,B
^FS

^FX Payment Block Line 1
^FO 533,56
^FB 228,1,0,C,0
^A0 N,20,20
^FD PRIORITY MAIL
^FS

^FX Payment Block Line 2
^FO 533,80
^FB 228,1,0,C,0
^A0 N,20,20
^FD U.S. POSTAGE PAID
^FS

^FX Payment Block Line 3
^FO 533,104
^FB 228,1,0,C,0
^A0 N,20,20
^FD ACME WIDGETS
^FS

^FX Payment Block Line 4
^FO 533,128
^FB 228,1,0,C,0
^A0 N,20,20
^FD eVS
^FS

^FX Top horizontal line
^FO 0,203.2
^GB 812,1,4,B
^FS

^FX Service Text
^FO 0,219.2
^FB 750,1,0,C,0
^A0 N,52,61
^FD USPS PRIORITY MAIL
^FS

^FX Registered Trademark symbol
^FO 580,215.2
^GS N,65,65
^FD A
^FS

^FX Bottom horizontal line
^FO 0,267.2
^GB 812,1,4,B
^FS

^FX Return Address Line 1
^FO 15,286
^A0 N,24,24
^FD INTERNET SALES DEPARTMENT
^FS

^FX Return Address Line 2
^FO 15,314
^A0 N,24,24
^FD FAST AND EFFICIENT SUPPLY CO.
^FS

^FX Return Address Line 3
^FO 15,342
^A0 N,24,24
^FD 10474 COMMERCE BVLD DUPLEX B
^FS

^FX Return Address Line 4
^FO 15,370
^A0 N,24,24
^FD SILVER SPRING MD20910-9999
^FS

^FX Delivery Address Line 1
^FO 112,533
^A0 N,29,29
^FD RONALD RECEIVER
^FS

^FX Delivery Address Line 2
^FO 112,566
^A0 N,29,29
^FD C/O RICK RECIPIENT
^FS

^FX Delivery Address Line 3
^FO 112,599
^A0 N,29,29
^FD INTERNET PURCHASING OFFICE
^FS

^FX Delivery Address Line 4
^FO 112,632
^A0 N,29,29
^FD BIG AND GROWING BUSINESS, CO.
^FS

^FX Delivery Address Line 5
^FO 112,665
^A0 N,29,29
^FD 8403 LEE HIGHWAY
^FS

^FX Delivery Address Line 6
^FO 112,698
^A0 N,29,29
^FD MERRIFIELD VA 22082-9999
^FS

^FX IM Barcode Top Line
^FO 0,811.2
^GB 812,1,15,B
^FS

^FX USPS Tracking Text
^FO 0,837.2
^FB 812,1,0,C,0
^A0 N,32,32
^FD USPS SIGNATURE TRACKING #
^FS

^FX IM Barcode
^FO 25,888.2
^BY 3
^BC N,152,N,N,N,D
^FD 4202208<89205591234567803239401
^FS

^FX IM Readable Text
^FO 0,1066
^FB 812,1,0,C,0
^A0 N,32,32
^FD 9205 5912 3456 7803 2394 01
^FS

^FX IM Barcode Bottom Line
^FO 0,1102.2
^GB 812,1,15,B
^FS

^FX Reset
^LT 0
^LS 0

^XZ